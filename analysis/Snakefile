MODELS = ["whn", "dtypes"]
SIMS = ["2pop", "commuting"]

subworkflow db:
    workdir: "../db"
    snakefile: "../db/Snakefile"

rule all:
    input:
        expand("fig/{model}_2pop.pdf", model=MODELS),
        expand("results/{x}.tsv", x=["2pop_reduction", "epsilon0"])

rule empirical:
    output: "fig/cross_sectional.pdf"
    input:
        db("../db/us/adjacency.tsv"), db("../db/us/commuting.tsv"),
        db("../db/europe/adjacency.tsv"), db("../db/europe/flights.tsv"),
        expand("../data/{x}/data.tsv", x=["ecdc", "marketscan", "nhsn"]),
        script="empirical_analysis.R"
    shell: "./{input.script}"

rule simulation_tables:
    output: expand("results/{x}.tsv", x=["2pop_reduction", "epsilon0"])
    input: expand("results/{model}_{sim}.rds", model=MODELS, sim=SIMS), script="tables.R"
    shell: "./{input.script}"

rule twopop_fig:
    output: expand("fig/{model}_2pop.pdf", model=MODELS)
    input: expand("results/{model}_2pop.rds", model=MODELS), "utils.R", script="2pop_figs.R"
    shell: "./{input.script}"

rule sim:
    output: expand("results/{{model}}_{sim}.rds", sim=SIMS)
    input: "{model}_model.R", "utils.R", script="{model}_sims.R"
    shell: "./{input.script}"
