MODELS = ["whn", "dtypes"]
SIMS = ["2pop", "commuting"]

rule all:
    input:
        expand("fig/{model}_2pop.pdf", model=MODELS),
        "fig/commuting.pdf",
        expand("results/{x}.tsv", x=["power", "2pop_reduction", "commuting_reduction", "epsilon0"])

rule empirical:
    output: "fig/cross_sectional.pdf"
    input: script="empirical_analysis.R"
    shell: "./{input.script}"

rule simulation_tables:
    output: expand("results/{x}.tsv", x=["2pop_reduction", "epsilon0"])
    input: expand("results/{model}_{sim}.rds", model=MODELS, sim=SIMS), script="tables.R"
    shell: "./{input.script}"

rule commuting_analysis:
    output: "fig/commuting.pdf", "results/commuting_reduction.tsv"
    input: expand("results/{model}_commuting.rds", model=MODELS), "utils.R", script="commuting_analysis.R"
    shell: "./{input.script}"

rule twopop_fig:
    output: expand("fig/{model}_2pop.pdf", model=MODELS)
    input: expand("results/{model}_2pop.rds", model=MODELS), "utils.R", script="2pop_figs.R"
    shell: "./{input.script}"

rule sim:
    output: expand("results/{{model}}_{sim}.rds", sim=SIMS)
    input: "{model}_model.R", "utils.R", script="{model}_sims.R"
    shell: "./{input.script}"

rule power:
    output: "results/power.tsv"
    input: script="power.R"
    shell: "./{input.script}"
